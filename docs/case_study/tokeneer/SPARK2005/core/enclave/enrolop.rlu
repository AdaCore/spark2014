/*----------------------------------------------------------------------
--  User rules for procedure Enclave.EnrolOp.
--
--  File name: enrolop.rlu
--
--  Description:
--    Procedure rule file.
--
--  Author: Phil Thornley, April 2009.  See www.sparksure.com
--
-- Copyright (c) 2009, Phil Thornley
-- All rights reserved.
--
-- Redistribution and use in source and binary forms, with or without
-- modification, are permitted provided that the following conditions
-- are met:
--
--  * Redistributions of source code must retain the above copyright
--    notice, this list of conditions and the following disclaimer.
--
--  * Redistributions in binary form must reproduce the above copyright
--    notice, this list of conditions and the following disclaimer in
--    the documentation and/or other materials provided with the distribution.
--
-- THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
-- "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
-- LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
-- PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER
-- OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
-- EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
-- PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
-- PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
-- LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
-- NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
-- SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
----------------------------------------------------------------------*/


/*----------------------------------------------------------------------
--  Rule 1
--
--  Justification:
--    Case 1: B1 is True directly and B2 is False by (2) and (4).
--    Case 2: B1 is False directly and B2 is True by (3) and (4).
----------------------------------------------------------------------*/
enrolop_user(1): B1 <-> not B2
                   may_be_deduced_from
                 [ S = St_3 and B1 or                 /* Case 1 */
                   S = St_2 and not B1,               /* Case 2 */
                   St_2 < St_3,                       /*  (2)   */
                   St_1 < St_2,                       /*  (3)   */
                   B2 <-> St_1 <= S and S <= St_2 ] . /*  (4)   */


/*----------------------------------------------------------------------
--  Rule 2
--
--  Justification:
--    Case 1: S = St_3 directly
--    Case 2: B1 is True by (2) and (3).
----------------------------------------------------------------------*/
enrolop_user(2): B1 or S = St_3
                   may_be_deduced_from
                 [ S = St_3 and B2 or                 /* Case 1 */
                   S = St_2 and B3,                   /* Case 2 */
                   St_1 < St_2,                       /*  (2)   */
                   B1 <-> St_1 <= S and S <= St_2 ] . /*  (3)   */


/*----------------------------------------------------------------------
--  Rule 3
--
--  Justification:
--    From (1) the conclusion reduces to: B2.
--    For both cases in (2) B2 follows from (3) and (4).
----------------------------------------------------------------------*/
enrolop_user(3): B1 <-> not B2
                   may_be_deduced_from
                 [ not B1,                            /* (1) */
                   S = St_2 or S = St_1,              /* (2) */
                   St_1 < St_2,                       /* (3) */
                   B2 <-> St_1 <= S and S <= St_2 ] . /* (4) */


/*----------------------------------------------------------------------
--  Rule 4
--
--  Justification:
--    For both cases in (1), B1 is True by (2) and (3).
----------------------------------------------------------------------*/
enrolop_user(4): B1 or B2
                   may_be_deduced_from
                 [ S = St_2 or S = St_1,              /* (1) */
                   St_1 < St_2,                       /* (2) */
                   B1 <-> St_1 <= S and S <= St_2 ] . /* (3) */
