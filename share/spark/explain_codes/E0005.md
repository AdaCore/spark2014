Example of illegal code:

```ada
X : Integer;

function F return Integer is
begin
   X := 1; -- error: write to global variable in function
   return 2;
end F;
```

This error is issued on functions which may write variables in the enclosing
scopes. This side-effect is not allowed in functions in SPARK, as this could
lead to different behaviors depending on the choice made by the compiler in the
order of evaluation of sub-expressions.

The fix is to change the function into a procedure, with an additional `out`
parameter for the function result:

```ada
X : Integer;

procedure P (Result : out Integer) is
begin
   X := 1;
   Result := 2;
end P;
```

Note that the same error is issued when reading a volatile variable in the
enclosing scope, unless its volatile property `Effective_Reads` is set to
`False`, as just a read of the variable is considered to have an effect on the
value of the variable in that case.

Another possibility, if the side-effect is not relevant for analysis, is to
hide it in a procedure marked as not in SPARK with aspect `SPARK_Mode =>
Off`. For example, if the effect above is only meant only for logging or
debugging, it could be isolated in a procedure `Hide`:

```ada
X : Integer;

procedure Hide
  with Global => null, Always_Terminates;

procedure Hide
  with SPARK_Mode => Off
is
begin
   X := 1;
end Hide;

function F return Integer is
begin
   Hide;
   return 2;
end F;
```

Note the contracts on `Hide` to indicate that this procedure does not read or
write any global variable and always terminates, which should be validated by
review on the body of `Hide`.
